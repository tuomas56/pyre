#!/usr/bin/env ipyre -a load -f

#(c) Tuomas Laakkonen 2015, under the MIT license.
#An example program that has the user enter some text and splits it into
#lines of size twenty, justifing it.
do
	let justify = def (text, n) do
		let words = text.split(" ").reverse!
		let mut lines = list!

		#first pass, add words to lines, ensuring they
		#don't pass the line limit and they have at least one space between them
		while words.len!.gt(0) do
			let mut remaining = n
			let mut line = list!

			while remaining.gt(0).and(words.len!.gt(0)) do
				if words.get(-1).len!.add(1).gt(remaining)
					break
				else do
					line.append(words.pop!.concat(" "))
					let remaining = remaining.sub(line.get(-1).len!)
				end
			end
			lines.append(line)
		end

		#second pass, add spaces to make the line lengths match up
		let lines = for line in lines do
			let mut remaining = n.sub(line.map(def (x) x.len!).apply(sum))
			let mut avg_spacing = remaining.div(line.len!).int!
			
			for j in range(line.len!.sub(1)) do
				line.set(j, line.get(j).concat(" ".rep(avg_spacing)))
			end

			let mut remainder = remaining.sub(avg_spacing.mul(line.len!.sub(1)))
			line.set(-1, " ".rep(remainder).concat(line.get(-1)))

			line.join("")
		end

		lines.join("\n")
	end

	print(justify(input("enter some text: "), 20))
end